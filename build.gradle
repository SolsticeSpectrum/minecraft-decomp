plugins {
    id 'base'
    id 'idea'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.10'
}

allprojects {
    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    version = '1.21.11'

    repositories {
        mavenCentral()
        maven { url 'https://libraries.minecraft.net' }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-Xlint:-options'
    }
}

ext {
    libs = [
        'authlib-7.0.61.jar', 'brigadier-1.3.10.jar', 'datafixerupper-9.0.19.jar',
        'gson-2.13.2.jar', 'guava-33.5.0-jre.jar', 'fastutil-8.5.18.jar',
        'netty-common-4.2.7.Final.jar', 'netty-buffer-4.2.7.Final.jar',
        'joml-1.10.8.jar', 'log4j-api-2.25.2.jar', 'slf4j-api-2.0.17.jar',
    ] as Set

    urls = [
        vf: 'https://github.com/Vineflower/vineflower/releases/download/1.11.2/vineflower-1.11.2.jar',
        client: 'https://piston-data.mojang.com/v1/objects/4509ee9b65f226be61142d37bf05f8d28b03417b/client.jar',
        server: 'https://piston-data.mojang.com/v1/objects/3ca78d5068bf9b422f694d3f0820e289581c0f0d/server.jar',
        assets: 'https://piston-meta.mojang.com/v1/packages/aaf4be9d6e197c384a09b1d9c631c6900d1f077c/29.json',
    ]

    assetsVer = '29'
}

task download {
    group = 'setup'

    doLast {
        file('jars').mkdirs()
        file('libs').mkdirs()

        Jars.download(urls.vf, file('vineflower.jar'))
        Jars.download(urls.client, file('jars/client.jar'))

        if (!file('jars/server.jar').exists()) {
            def b = file('jars/server-bundler.jar')
            Jars.download(urls.server, b)
            Jars.unbundle(b, file('jars/server.jar'), file('libs'), libs)
        }
    }
}

task decompServer(type: Exec) {
    group = 'decompile'
    dependsOn download
    doFirst { delete 'decompSrc/server' }

    commandLine = Setup.vfArgs(libs) + ["jars/server.jar", "decompSrc/server"]
}

task decompClient(type: Exec) {
    group = 'decompile'
    dependsOn download
    doFirst { delete 'decompSrc/client' }

    commandLine = Setup.vfArgs(libs) + ["jars/client.jar", "decompSrc/client"]
}

task patchServer {
    group = 'patches'
    dependsOn decompServer
    doLast {
        Setup.copySrc(rootDir, 'server')
        Patcher.apply(file('server/src/main/java'), file('patches/server.patch'))
    }
}

task patchClient {
    group = 'patches'
    dependsOn decompClient
    doLast {
        Setup.copySrc(rootDir, 'client')
        Patcher.apply(file('client/src/main/java'), file('patches/client.patch'))
    }
}

task snapServer {
    group = 'setup'
    dependsOn patchServer
    doLast {
        Patcher.snap(file('server/src/main/java'), file('patchSrc/server-base.zip'))
    }
}

task snapClient {
    group = 'setup'
    dependsOn patchClient
    doLast {
        Patcher.snap(file('client/src/main/java'), file('patchSrc/client-base.zip'))
    }
}

task modApply {
    group = 'mods'
    doLast {
        def p = project.hasProperty('args') ? project.args.split(',') : null
        if (!p || p.length < 2) throw new RuntimeException("Usage: -Pargs=side,mod1,mod2")
        Mod.apply(rootDir, p[0], p[1..-1] as String[])
    }
}

task modRevert {
    group = 'mods'
    doLast {
        def p = project.hasProperty('args') ? project.args.split(',') : null
        if (!p || p.length < 2) throw new RuntimeException("Usage: -Pargs=side,mod1,mod2")
        Mod.revert(rootDir, p[0], p[1..-1] as String[])
    }
}

task modGen {
    group = 'mods'
    doLast {
        def p = project.hasProperty('args') ? project.args.split(',') : null
        if (!p || p.length != 2) throw new RuntimeException("Usage: -Pargs=side,name")
        Mod.gen(rootDir, p[0], p[1])
    }
}

task modPackClient {
    group = 'mods'
    dependsOn ':client:compileJava'
    doLast { Mod.pack(rootDir, 'client') }
}

task modPackServer {
    group = 'mods'
    dependsOn ':server:compileJava'
    doLast { Mod.pack(rootDir, 'server') }
}

task setupServer { group = 'setup'; dependsOn snapServer }
task setupClient { group = 'setup'; dependsOn snapClient }
task setup { group = 'setup'; dependsOn setupServer, setupClient }

task genPatchServer {
    group = 'patches'
    doLast {
        Patcher.diff(file('decompSrc/server'), file('server/src/main/java'),
            file('patches/server.patch'), false)
    }
}

task genPatchClient {
    group = 'patches'
    doLast {
        Patcher.diff(file('decompSrc/client'), file('client/src/main/java'),
            file('patches/client.patch'), false)
    }
}

subprojects {
    jar {
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
        from(zipTree("${rootDir}/jars/${project.name}.jar")) { include '**/package-info.class' }

        manifest {
            def main = project.name == 'server' ? 'net.minecraft.server.Main' : 'net.minecraft.client.main.Main'
            attributes 'Main-Class': main
        }

        doFirst {
            def cp = configurations.runtimeClasspath.collect { 'libs/' + it.name }.join(' ')
            manifest { attributes 'Class-Path': cp }
        }
    }

    task copyDeps(type: Copy) {
        from configurations.runtimeClasspath
        into "${buildDir}/libs/libs"
    }

    build.dependsOn copyDeps

    dependencies {
        implementation 'com.mojang:authlib:7.0.61'
        implementation 'com.mojang:brigadier:1.3.10'
        implementation 'com.mojang:datafixerupper:9.0.19'
        implementation 'com.mojang:jtracy:1.0.37'
        implementation 'com.mojang:logging:1.6.11'
        implementation 'com.google.code.gson:gson:2.13.2'
        implementation 'com.google.guava:guava:33.5.0-jre'
        implementation 'io.netty:netty-codec-http:4.2.7.Final'
        implementation 'io.netty:netty-transport-classes-epoll:4.2.7.Final'
        implementation 'io.netty:netty-transport-classes-kqueue:4.2.7.Final'
        implementation 'com.microsoft.azure:msal4j:1.23.1'
        implementation 'it.unimi.dsi:fastutil:8.5.18'
        implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
        implementation 'org.joml:joml:1.10.8'
        implementation 'at.yawk.lz4:lz4-java:1.8.1'
        implementation 'com.github.oshi:oshi-core:6.9.0'
    }
}

task runServer(type: JavaExec) {
    group = 'run'
    doFirst { mkdir 'run_server' }

    mainClass = 'net.minecraft.server.Main'
    classpath = project(':server').sourceSets.main.runtimeClasspath
    workingDir = file('run_server')

    args = ['nogui']
}

task runClient(type: JavaExec) {
    group = 'run'
    dependsOn 'assets'
    doFirst { mkdir 'run' }

    mainClass = 'net.minecraft.client.main.Main'
    classpath = project(':client').sourceSets.main.runtimeClasspath
    workingDir = file('run')

    environment '__GL_THREADED_OPTIMIZATIONS', '0'

    args = ['--version', version, '--accessToken', '0', '--assetsDir', 'assets',
            '--assetIndex', assetsVer, '--gameDir', 'run']
}

idea.project.settings {
    runConfigurations {
        'Minecraft Client'(org.jetbrains.gradle.ext.Application) {
            mainClass = 'net.minecraft.client.main.Main'
            moduleName = "${rootProject.name}.client.main"
            workingDirectory = "${rootDir}/run"
            programParameters = "--version ${version} --accessToken 0 " +
                "--assetsDir assets --assetIndex ${assetsVer} --gameDir ${rootDir}/run"
            jvmArgs = '-XX:+AllowEnhancedClassRedefinition'
            envs = ['__GL_THREADED_OPTIMIZATIONS': '0']
        }

        'Minecraft Server'(org.jetbrains.gradle.ext.Application) {
            mainClass = 'net.minecraft.server.Main'
            moduleName = "${rootProject.name}.server.main"
            workingDirectory = "${rootDir}/run_server"
            programParameters = 'nogui'
            jvmArgs = '-XX:+AllowEnhancedClassRedefinition'
        }
    }
}

jar.enabled = false

clean {
    delete 'run', 'run_server', 'decompSrc', 'patchSrc'
    delete 'client/src', 'server/src', 'client/bin', 'server/bin'
    delete 'jars', 'libs', 'vineflower.jar'
}

task assets {
    group = 'assets'
    outputs.dir('run/assets/objects')
    doLast {
        Setup.assets(file('run'), assetsVer, urls.assets)
    }
}
